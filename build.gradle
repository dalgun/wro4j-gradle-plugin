buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

group = 'ro.isdc.wro4j.gradle'
version = '1.8.0.Beta4'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile ('ro.isdc.wro4j:wro4j-extensions:1.8.0') {
        exclude module: 'groovy-all'
    }
    compile 'org.mockito:mockito-all:1.10.19'
    compile 'commons-lang:commons-lang:2.6'

    runtime 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile ('com.netflix.nebula:nebula-test:4.0.0') {
        exclude module: 'groovy-all'
    }
}

bintray {
    user = System.getProperty('bintrayUser')
    key = System.getProperty('bintrayApiKey')
    pkg {
        repo = 'maven'
        name = 'wro4j-gradle-plugin'
        desc = 'Wro4J Gradle Plugin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/IlyaAI/wro4j-gradle-plugin.git'
        labels = ['wro4j', 'gradle', 'plugin']
    }
    publications = ['bintray']
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        bintray (MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}
